plugins {
  id "java"
  id "maven-publish"
  id "com.enonic.xp.base" version "3.6.1"
  id "com.github.node-gradle.node"  version "7.1.0"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

dependencies {
  compileOnly "com.enonic.xp:lib-admin:${xpVersion}"
  implementation "com.github.ua-parser:uap-java:1.6.1"
  implementation "io.sentry:sentry:6.20.0"
}

repositories {
  mavenLocal()
  mavenCentral()
  xp.enonicRepo()
}

node {
  download = true
  version = "22.12.0"
}

processResources {
  exclude "**/.gitkeep"
  exclude "**/*.json"
  exclude "**/*.ts"
  exclude "**/*.tsx"
}

tasks.register("dev", Exec) {
  if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
    commandLine "cmd", "/c", "gradlew.bat", "deploy", "-t"
  } else {
    commandLine "./gradlew", "deploy", "-t"
  }
}

tasks.register("npmBuild", NpmTask) {
  args = [
    "run",
    "--silent",
    "build"
  ]
  dependsOn npmInstall
  environment = [
    "FORCE_COLOR": "true",
    "LOG_LEVEL_FROM_GRADLE": gradle.startParameter.logLevel.toString(),
    "NODE_ENV": project.hasProperty("dev") || project.hasProperty("development") ? "development" : "production"
  ]
  inputs.dir "src/main/resources"
  outputs.dir "build/resources/main"
  outputs.upToDateWhen { false }
}

jar.dependsOn npmBuild

tasks.register("npmCheck", NpmTask) {
  dependsOn npmInstall
  args = [
    "run",
    "check"
  ]
  environment = [
    "FORCE_COLOR": "true",
  ]
}

tasks.withType(Copy).configureEach {
  includeEmptyDirs = false
}

publishing {
  repositories {
    maven {
      name = "itemtestRepository"
      url = "https://repo.itemtest.no/releases"
      credentials(PasswordCredentials)
      authentication {
        basic(BasicAuthentication)
      }
    }
  }
  publications {
    maven(MavenPublication) {
      from components.java
      groupId group
      artifactId projectName
      version version
    }
  }
}
